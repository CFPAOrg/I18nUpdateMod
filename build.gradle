buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven {
        name = "jei"
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

version = modVersion
group = "org.cfpa.i18nupdatemod" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "i18nupdatemod"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2738"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20180701"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace "@VERSION@", project.version
}

configurations {
    compile.extendsFrom shadow
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"

    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // the 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    //provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // the deobf configurations:  'deobfCompile' and 'deobfProvided' are the same as the normal compile and provided,
    // except that these dependencies get remapped to your current MCP mappings
    //deobfCompile 'com.mod-buildcraft:buildcraft:6.0.8:dev'
    //deobfProvided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    deobfCompile "mezz.jei:jei_${minecraft.version}:4.8.5.147"

    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpmime
    shadow 'org.apache.httpcomponents:httpmime:4.3.3'

    testCompile "junit:junit:4.12"
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "org.apache.http.entity.mime", "org.cfpa.i18nupdatemod.repack.org.apache.http.entity.mime"

    classifier = null
    dependencies {
        exclude(dependency('org.apache.httpcomponents:httpclient:4.3.3'))
        exclude(dependency('org.apache.httpcomponents:httpcore:4.3.2'))
        exclude(dependency('commons-codec:commons-codec:1.6'))
        exclude(dependency('commons-logging:commons-logging:1.1.3'))
    }
}
reobf {
    shadowJar { mappingType = 'SEARGE' }
}
tasks.reobfShadowJar.mustRunAfter shadowJar

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'FMLAT': 'i18nmod_at.cfg'
//        attributes 'ContainedDeps': 'httpmime-4.3.3.jar'
    }
}
